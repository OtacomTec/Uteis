<%
    Dim CfgSrc
    CfgSrc = "sample.cfg" ' Setting a default value for convenience
    If Request("CfgSrc").Count > 0 Then
        CfgSrc = Request("CfgSrc")(1)
    End If
    
    ' Enum Sub - used to list the loaded configuration below
    Sub EnumCfg(branch)
        Dim I,J,r
        For I = 1 To branch.Count
            If IsObject(branch(I)) Then
                ' Section or record
                If branch(I).Info.Type Then
                    ' Section
                    ' Recourse - this is a sub branch
                    %>
                    <LI>Section: <B><%= branch.Key(I) %></B> Class name: (<%= branch(I).Info.Class %>)<BR>
                    <UL>
                    <%
                    EnumCfg branch(I)
                    %>
                    </UL>
                    <%
                Else
                    ' Record
                    %>
                    <LI>Record: <B><%= branch.Key(I) %></B><BR>
                    Values: 
                    <%
                    Set r = branch(I)
                    For J = 1 to r.Count
                        Response.Write r(J)
                        If J < r.Count Then Response.Write ", "
                    Next
                    %>
                    </LI>
                    <%
                End If
                
            Else
                ' All the values should be packed in records. However to simplify
                ' the usage if a value is written directly into the section it will
                ' be packed automatically in the record with the same name when the
                ' consfirutation is stored.
            End If
        Next
    End Sub
%>
<HTML>
<HEAD>
<SCRIPT>
    function fillSample(n) {
        switch (n) {
            case 1:
                document.all("CfgSrc").value = "sample.cfg";
                document.all("CfgType").selectedIndex = 0;
            break;
            case 2:
                document.all("CfgSrc").value = "sample.bin";
                document.all("CfgType").selectedIndex = 1;
            break;
            case 3:
                document.all("CfgSrc").value = "Software\\Microsoft\\Internet Explorer";
                document.all("CfgType").selectedIndex = 2;
            break;
        }
    }
</SCRIPT>
</HEAD>
<BODY>
    <FORM METHOD="POST" ACTION="<%= Request.ServerVariables("SCRIPT_NAME") %>">
        Type the file name only - the file will be read from the same directory (the asp file directory). Or type
        a registry path (the sample assumes it is in CURRENT_USER registry branch).
        <HR>
        Sample values:<BR>
        Text file: <B onClick="fillSample(1)" style="cursor:hand">sample.cfg</B><BR>
        Binary file: <B onClick="fillSample(2)" style="cursor:hand">sample.bin</B> <I>But create it first with the other sample</I><BR>
        Registry: <B onClick="fillSample(3)" style="cursor:hand">Software\Microsoft\Internet Explorer</B> <I>Do not enter very big branches. The sample will enumerate
        the entire branch. Therefore a big registry branche will need a lot of time - the components are fast but the page is generated by a script ;).</I>
        <HR>
        Configuration to enum: <INPUT TYPE="TEXT" NAME="CfgSrc" VALUE="<%= CfgSrc %>"><BR>
        What is it:
            <SELECT NAME="CfgType">
                <OPTION VALUE="TXT">Text file</OPTION>
                <OPTION VALUE="BIN">Binary File</OPTION>
                <OPTION VALUE="REG">Registry path</OPTION>
            </SELECT>
        <BR>
        <INPUT TYPE="SUBMIT" NAME="Do" VALUE="List">
    </FORM>
    <HR>
    <%
    Dim cfgObj, bSuccess, cfg, binFile, sf
    If Request("Do").Count > 0 And CfgSrc <> "" Then
        Set cfgObj = Server.CreateObject("newObjects.utilctls.ConfigFile")
        bSuccess = False
        Select Case CStr(Request("CfgType"))
            Case "TXT"
                ' Reading from text file
                Set cfg = cfgObj.Read(Server.MapPath(CfgSrc))
                bSuccess = True
                ' ASP error will occur if the file is not found or some other file error happened
                ' The sample is simple thus we will not deal with the errors in order to avoid
                ' confusing code
            Case "BIN"
                Set sf = Server.CreateObject("newObjects.utilctls.SFMain")
                Set binFile = sf.OpenFile(Server.MapPath(CfgSrc))        
                Set cfg = cfgObj.ReadFromBinaryStream(binFile)
                bSuccess = True
            Case "REG"
                Set cfg = cfgObj.ReadFromRegistry(1,CfgSrc)
                bSuccess = True
        End Select
        
        If bSuccess Then
        %>
        <UL>
            <% EnumCfg cfg %>
        </UL>
        <%
        End If
    End If
%>
    
    
</BODY>
</HTML>